#!/usr/bin/env bds

system = 'cluster'
cpus = 2

timeout := 5 * day
iter := 100	# Number of iterations
prev := 0.08	# Diseace prevalencese

#---
# Combinations to analyze
#---
ns    := [ -1 ]		# Negative means 'use default numbers (there is a pre-defined sequence in the R script)
afs   := [0.01, 0.025, 0.05, 0.075, 0.10, 0.125, 0.15, 0.20]
betas := [0.1, 0.2, 0.3, 0.4, 0.5, 0.6, 0.7, 0.8, 0.9, 1.0, 1.5, 2.0, 3.0, 5.0]

logRegScript := "$ppwd/logisticRegressionPowerGT.r"
outDir := "$ppwd/logisticRegressionPowerGT/"
outDir.mkdir()

#-------------------------------------------------------------------------------
# Main
#-------------------------------------------------------------------------------

for( int n : ns ) {
	for( real af1 : afs ) {
		for( real af2 : afs ) {
			if( af1 < af2 )	continue

			for( real beta3 : betas ) {
				out := "$outDir/n_$n.af1_$af1.af2_$af2.beta3_$beta3.txt"
				if( !out.exists() )	{
					print "n:$n\taf1:$af1\taf2:$af2\tbeta3:$beta3\n"
					task Rscript --vanilla $logRegScript $iter $n $af1 $af2 $beta3 $prev > $out
				}
			}
		}
	}
}
