#!/usr/bin/env bds

system = 'cluster'
cpus = 2

iter := 1000	# Number of iterations
prev := 0.08	# Disease prevalencense

#---
# Combinations to analyze
#---
#ns    := [1000000, 500000, 100000, 50000, 20000, 10000, 1000]
#afs   := [0.2, 0.1, 0.05, 0.01]
#betas := [0.1, 0.2, 0.5, 1, 2, 5]
ns    := [10000, 250000]
afs   := [0.01, 0.02, 0.03, 0.04, 0.05, 0.06, 0.07, 0.08, 0.09, 0.10, 0.11, 0.12, 0.13, 0.14, 0.15, 0.16, 0.17, 0.18, 0.19, 0.20]
betas := [0.1, 0.2, 0.3, 0.4, 0.5, 0.6, 0.7, 0.8, 0.9, 1.0, 1.1, 1.2, 1.3, 1.4, 1.5, 1.6, 1.7, 1.8, 1.9, 2.0]

logRegScript := "$ppwd/logisticRegressionPowerGT.r"
outDir := "$ppwd/logisticRegressionPowerGT/"
outDir.mkdir()

#-------------------------------------------------------------------------------
# Main
#-------------------------------------------------------------------------------

for( int n : ns ) {
	for( real af1 : afs ) {
		for( real af2 : afs ) {
			if( af1 < af2 )	continue

			for( real beta3 : betas ) {
				out := "$outDir/n_$n.af1_$af1.af2_$af2.beta3_$beta3.txt"
				if( !out.exists() )	{
					print "n:$n\taf1:$af1\taf2:$af2\tbeta3:$beta3\n"
					task Rscript --vanilla $logRegScript $iter $n $af1 $af2 $beta3 $prev > $out
				}
			}
		}
	}
}
