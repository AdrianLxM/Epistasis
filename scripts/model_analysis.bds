#!/usr/bin/env bds

#-------------------------------------------------------------------------------
# Epistasis project: Model analysis
#
#															Pablo Cingolani 2014
#-------------------------------------------------------------------------------

#-------------------------------------------------------------------------------
# Analize gene-gene interaction log-likelihood(MSA)
#-------------------------------------------------------------------------------

void geneGeneLogLik(string dirOut, string interactions) {
	println "\nEstimate log-Likelihood for all gene-gene combinations: $likAll"

	dirOut.mkdir()
	if( system == 'local' ) {
		# Launch one process
		out := "$dirOut/likelihood.txt"
		task( out <- interactions ) {
			sys $cmdEpistasis likelihoodAll -1 $tree $msaBest $idMapBest $qhat $aaFreqs $qhat2 $aa2Freqs $interactions $dirOut > $out
		}
	} else {
		# Read interactions and split into small files
		pairs := readInteractions( interactions, dirOut )

		# Split into small lists
		sys rm -vf $dirOut/genes_pairs_list.* || true
		filesPairs := pairs.splitToFile( "$dirOut/genes_pairs_list.", llGeneLines )

		# Launch a task for eah file
		for( string file : filesPairs ) {
			out := "$dirOut/likelihood." + file.baseName()
			println "\t$out"
			task( out <- file ) {
				sys $cmdEpistasis likelihoodAll $cpus $tree $msaBest $idMapBest $qhat $aaFreqs $qhat2 $aa2Freqs $file $dirOut > $out
			}
		}
	}
}

#-------------------------------------------------------------------------------
# Calculate best scores from pre-calculated LL(MSA) on gene-gene pairs
#-------------------------------------------------------------------------------
void geneGeneLogLikMatrix(string geneGeneDir) {
	geneGene := sys find $geneGeneDir -iname "*.txt"
	geneGeneList := geneGene.lines()

	print "calculating likelihoodGeneGeneMatrix: '$geneGeneDir'\n"
	maxSplit := 10
	for( int split=0 ; split < maxSplit ; split++ ) {
		sl := geneGeneList.split(split, maxSplit)
		files := sl.join(" ")
		len := sl.size()

		out := "$geneGeneDir/likelihoodGeneGeneMatrix.$split.$maxSplit.txt"
		print "\t$out\tnumber of files: $len\n"
		task( out <- sl ) {
			sys $cmdEpistasis likelihoodGeneGeneMatrix $tree $msaBest $files | tee $out
		}
	}
}

#-------------------------------------------------------------------------------
# Likelihood for interactions from a variant in a VCF file and all other 
# sites withn a protein
#-------------------------------------------------------------------------------

void likelihoodvcf(string vcfFile, string dirOut, int numSplits) {
	println "\nLogLikelihood of MSA (interaction) using VCF file '$vcfFile'"
	dirOut.mkdir()

	for( int i=0 ; i < numSplits ; i++ ) {
		println "\t$i / $numSplits"

		base := vcfFile.baseName(".vcf")
		outputFile := "$dirOut/likelihood.$numSplits.$i.txt"
		task( outputFile <- [vcfFile, msaBest, qhat, qhat, aaFreqs, aa2Freqs] ) {
			sys $cmdEpistasisXL likelihoodvcf $cpus $tree $msaBest $qhat $aaFreqs $qhat2 $aa2Freqs $snpeffConfig $genome $vcfFile $numSplits $i $dirOut > $outputFile
		}
	}
}

#-------------------------------------------------------------------------------
# Read gene-gene interactions
#-------------------------------------------------------------------------------
string[] readInteractions(string intFile, string dirGenesLike) {
	string[] pairs
	string gene1, gene2

	# Keep interactions that haven't been calculated so far
	for( string gg : intFile.read().lines() ) {
		(gene1, gene2) = gg.split('\t')
		if( gene2 < gene1 )		(gene1, gene2) = [gene2, gene1]

		# Does 'gene likelihood' file exist?
		if( ! "$dirGenesLike/$gene1/$gene2.txt".exists() )	pairs += "$gene1\t$gene2"
	}

	return pairs
}

#-------------------------------------------------------------------------------
# Get part of 'splitNum' out of 'maxSplitNum' from a list
#-------------------------------------------------------------------------------
string[] split(string[] list, int splitNum, int maxSplitNum) {
	string[] newList
	idx := 0

	for(string s : list) {
		if( (idx % maxSplitNum) == splitNum )	newList += s
		idx++
	}

	return newList
}

#-------------------------------------------------------------------------------
# Split a list into small files
# Return file names
#-------------------------------------------------------------------------------
string[] splitToFile( string[] list, string baseName, int linesPerFile) {
	println "Splitting into $linesPerFile line per file:"
	string[] fileNames
	string[] thisFileLines

	# Split liens
	for( string line : list ) {
		# Enough lines to create a new file?
		if( thisFileLines.size() >= linesPerFile ) {
		 	fileName := baseName + fileNames.size()
			fileName.write( thisFileLines.join("\n") )
			fileNames += fileName
			thisFileLines = []
		}

		thisFileLines += line
	}

	# Write last list
	if( ! thisFileLines.isEmpty() ) {
	 	fileName := baseName + fileNames.size()
		fileName.write( thisFileLines.join("\n") )
		fileNames += fileName
	}

	return fileNames
}

#-------------------------------------------------------------------------------
# Analysis of nextprot annotations
#-------------------------------------------------------------------------------

void analysisNextProt() {
	# Annotations of amino acids "in contact" using NextProt
	println "\nAnnotating with NextProt: '$aaNextProt'"
	task( aaNextProt <- aaContactBest )	sys $cmdEpistasis nextprot $snpeffConfig $genomeEnsembl $idMapBest $aaContactBest > $aaNextProt
	wait
}

#-------------------------------------------------------------------------------
# Statistics, MI, VarInf
#-------------------------------------------------------------------------------
void analysisMiVarinf() {

	# Calculate values (MI, VarInf, etc.) for AA sequeces 'in contact'
	for( string type : ['mi', 'varInf'] ) {
		# Single base
		aaStats := "$dir/aa.contact.$type.txt"
		aaStatsSummary := "$dir/aa.contact.$type.summary.txt"
		println "\nCalculating $type:\n\t'$aaStats'\n\t'$aaStatsSummary'"
		task( aaStats <- aaNextProt )	sys $cmdEpistasis aaContactStats $type $aaNextProt > $aaStats 2> $aaStatsSummary

		# Three bases
		aaStats3 := "$dir/aa.contact.$type.3.txt"
		aaStatsSummary3 := "$dir/aa.contact.$type.3.summary.txt"
		println "\nCalculating $type (3 bases):\n\t'$aaStats3'\n\t'$aaStatsSummary3'"
		task( aaStats3 <- aaNextProt )	sys $cmdEpistasis aaContactStatsN $type 1 $tree $msa $idMapBest $aaNextProt > $aaStats3 2> $aaStatsSummary3
	}
	wait

	# Create files for R
	println "Create R files"
	aaStats := "$dir/aa.contact.mi.txt"
	aaStatsR := "$dir/aa.contact.stats.txt";
	task( aaStatsR <- aaStats ) {
		sys echo -e "mi\thxy\tvarinf\thcond.xy\thcond.yx\thx\thy\tcons.x\tcons.y" > $aaStatsR
		sys cat $aaStats | cut -f 19- >> $aaStatsR
	}

	# Create R data files
	for( string type : ['mi', 'varInf'] ) {
		aaStats3 := "$dir/aa.contact.$type.3.txt"
		aaStats3R := "$dir/aa.contact.$type.3.vals.txt"

		println "\nCreating R data file  $type (3 bases): '$aaStats3R'"
		task( aaStats3R <- aaStats3 ) {
			sys echo $type > $aaStats3R
			sys cut -f 19 $aaStats3 >> $aaStats3R
		}
	}
	wait

	# Calculate background distribution for MI and VarInf
	for( string type : ['mi', 'varInf'] ) {
		# Single base background model
		bg := "$dir/bg.$type.summary.txt"
		bgDetails := "$dir/bg.$type.details.txt"
		println "\nCalculating background distribution ($bgNum iterations): '$bg'"
		task( bgDetails <- [tree, msa] ) sys $cmdEpistasis background $type 0 $bgNum $tree $msa $idMapBest 2>$bg >$bgDetails
		wait

		# Three bases background model
		bg3 := "$dir/bg.$type.3.summary.txt"
		bg3Details := "$dir/bg.$type.3.details.txt"
		println "\nCalculating background distribution ($bgNum iterations): '$bg3'"
		task( bg3Details <- [tree, msa] ) sys $cmdEpistasis background $type 1 $bgNum $tree $msa $idMapBest 2>$bg3 >$bg3Details
		wait
	}

	# Calculate conservation statistics for many AA windows sizes
	aaCons := "$dir/aa.conservation.txt"
	println "\nCalculating conservation: '$aaCons'"
	task( aaCons <- [tree, msa, aaContactBest] )	sys $cmdEpistasis conservation $tree $msa $idMapBest $aaContactBest | tee $aaCons
	wait

	# Create a BED file from MSA coordinates. To show coverage, load this BED file in IGV.
	msaBed := "$dir/" + msa.baseName().swapExt(".fa", ".bed")
	println "\nCreatig BED file: '$msaBed'"
	task( msaBed <- msa )	sys cat $msa | grep _hg19 | cut -f 5 -d " " | tr -d "+" | sed "s/-$//" | tr ":-" "\t\t" > $msaBed
	wait
}

#-------------------------------------------------------------------------------
# AA frequencies, Transitions matrices, AA-pairs transitions
#-------------------------------------------------------------------------------
void analysisTransitions() {

	aaContactMISumm := "$dir/aa.contact.mi.summary.txt"
	aaContactVISumm := "$dir/aa.contact.varInf.summary.txt"
	aaCountMatrix := "$dir/aa.count.matrix.txt"
	aaMiMatrix := "$dir/aa.mi.matrix.txt"
	aaViMatrix := "$dir/aa.vi.matrix.txt"
	task( aaCountMatrix <- aaContactMISumm ) {
		sys cat $aaContactMISumm | grep -w COUNT_AA                      | cut -f 2-  | tr "-" "\t" | $cmdTable2Matrix | tee $aaCountMatrix
		sys cat $aaContactMISumm | grep -w COUNT_AA_NON_FULL_CONS_mi     | cut -f 2,5 | tr "-" "\t" | $cmdTable2Matrix | tee $aaMiMatrix
		sys cat $aaContactVISumm | grep -w COUNT_AA_NON_FULL_CONS_varInf | cut -f 2,5 | tr "-" "\t" | $cmdTable2Matrix | tee $aaViMatrix
	}

	# Calculate AA-pairs transitions for 'AA in contact'. Also calculate 'null' distribution
	# Note: These are 400x400 matrices 
	trM := "$dir/transitions.txt"
	println "\nCalculating transitions ($bgNum iterations): '$trM'"
	task( trM <- [tree, msa, aaContactBest] )	{
		sys $cmdEpistasis transitions $bgNum $tree $msa $idMapBest $aaContactBest > $trM 
		sys grep AA_SINGLE_IN_CONTACT $trM		| cut -f 2- > $dir/transitions.aa_single.in_contact.txt
		sys grep AA_SINGLE_BG $trM				| cut -f 2- > $dir/transitions.aa_single.bg.txt
		sys grep AA_PAIRS_IN_CONTACT $trM		| cut -f 2- > $dir/transitions.aa_pairs.in_contact.txt
		sys grep AA_PAIRS_BG_RAND $trM			| cut -f 2- > $dir/transitions.aa_pairs.bg_rand.txt
		sys grep AA_PAIRS_BG_WITHIN_PROT $trM	| cut -f 2- > $dir/transitions.aa_pairs.bg_within_prot.txt
	}

	wait

	# Calculate AA frequencies
	println "\nCalculating AA frequencies: '$aaFreqs'"
	task( aaFreqs <- [tree, msa, idMapBest] )	sys $cmdEpistasis aaFreqs $tree $msa $idMapBest > $aaFreqs
	wait
}

#-------------------------------------------------------------------------------
# Likelihood from epistatic model
#-------------------------------------------------------------------------------
void analysisLikelihood() {

	println "\nEstimate log-Likelihood: '$lik'"
	task( lik <- [tree, msaBest, idMapBest, aaContactBest, qhat, aaFreqs, qhat2, aa2Freqs] ) {
		sys $cmdEpistasis likelihood $tree $msaBest $idMapBest $aaContactBest $qhat $aaFreqs $qhat2 $aa2Freqs | tee $lik
	}

	println "\nEstimate log-Likelihood null distribution: $likNull"
	task( likNull <- [tree, msaBest, idMapBest, qhat, aaFreqs, qhat2, aa2Freqs] ) {
		sys $cmdEpistasis likelihoodNull $bgNum $tree $msaBest $idMapBest $qhat $aaFreqs $qhat2 $aa2Freqs > $likNull
	}

	println "\nEstimate log-Likelihood (3-AA neighborhood): '$lik3'"
	task( lik3 <- [tree, msaBest, idMapBest, aaContactBest, qhat, aaFreqs, qhat2, aa2Freqs] ) {
		sys $cmdEpistasis likelihood3 $tree $msaBest $idMapBest $aaContactBest $qhat $aaFreqs $qhat2 $aa2Freqs | tee $lik3
	}

	bgNum = 100 * 1000	# Number of iterations for background calculation
	println "\nEstimate log-Likelihood null distribution (3-AA neighborhood): $likNull3"
	task( likNull3 <- [tree, msaBest, idMapBest, qhat, aaFreqs, qhat2, aa2Freqs] ) {
		sys $cmdEpistasis likelihoodNull3 $bgNum $tree $msaBest $idMapBest $qhat $aaFreqs $qhat2 $aa2Freqs | tee $likNull3
	}
}

#-------------------------------------------------------------------------------
# Gene-Gene Log likelihood (MSA) analysis: All possible pairs
#-------------------------------------------------------------------------------
void analysisGeneGene() {
	geneGeneLogLik("$dir/interactions/alt", interactions)
	geneGeneLogLik("$dir/interactions/null", interactionsNull)
}

#-------------------------------------------------------------------------------
# Do ClinVar entries have better LL_MSA than 'null'?
#-------------------------------------------------------------------------------
void analysisClinvar() {
	println "\nClinVar analysis"
	clinVar := "$dir/clinvar-latest.pass.vcf.gz"
	dirOut := "$dir/likelihood.clinvar"
	numSplits := 100
	likelihoodvcf(clinVar, dirOut, numSplits)

	# Use dbSnp entries from 1000 Genomes (this should be the 'null' distribution to compare clinVar)
	println "\nLogLikelihood of MSA (interaction) using 1000-Genomes project"
	g1k := "$dir/g1k.subsample.vcf"
	dirOut = "$dir/likelihood.g1k"
	likelihoodvcf(g1k, dirOut, numSplits)

	# Use dbSnp entries from 1000 Genomes (this should be the 'null' distribution to compare clinVar)
	println "\nLogLikelihood of MSA (interaction) using HGMD"
	hgmd := "$dir/HGMD.vcf"
	dirOut = "$dir/likelihood.hgmd"
	numSplits = 200
	likelihoodvcf(hgmd, dirOut, numSplits)
}

#-------------------------------------------------------------------------------
# Interacting vs non interacting genes LL(MSA) statistics
#-------------------------------------------------------------------------------
void analysisInteracting() {
	geneGeneLogLikMatrix("$dir/interactions/alt")
	geneGeneLogLikMatrix("$dir/interactions/null")
}

#-------------------------------------------------------------------------------
# Find LL(MSA) on interacting proteins (COMPOUND entries in pdb)
#-------------------------------------------------------------------------------
void analysisCompountProteins() {

	# Select a set of pdb entries having interacting protein structures ('COMPOUND' entries)
	# Find ID map for those entries
	pdbCompound := "$dir/pdbCompoundLines.human.txt"
	pdbCompoundList := "$dir/pdbCompoundLines.human.pdb_list.txt"
	idMapPdbCompound := "$dir/idMap_ensemblId_refseq_pdbId.pdbCompound.txt"
	msasCompound := "$dir/msas.pdb_compound.fa"

	task( [pdbCompound, pdbCompoundList, idMapPdbCompound] <- pdbDir ) {
		sys $cmdPdbCompoundLines $pdbDir > $pdbCompound
		sys cut -f 1 $pdbCompound | tr "[a-z]" "[A-Z]" | sort > $pdbCompoundList
		sys grep -w -f $pdbCompoundList $idMapConfirmed > $idMapPdbCompound
	}

	task( msasCompound <- [tree, msa, idMapPdbCompound] )	sys $cmdEpistasisXL filterMsa $tree $msa $idMapPdbCompound > $msasCompound

	# Calculate LL(MSA) of AA in contact vs AA not in contact
	# USe different neighborhood values
	for(int neigh=1; neigh < 5 ; neigh++ ) {
		llCompound := "$dir/likelihood.pdb_compound.neigh_$neigh.$aaContactDist.txt"
		task( llCompound <- [pdbCompoundList, idMapPdbCompound, msasCompound], cpus := 1) {
			sys $cmdEpistasis likelihoodpdbinteract $snpeffConfig $genome $tree $msasCompound $pdbDir $idMapConfirmed $qhat $aaFreqs $qhat2 $aa2Freqs $pdbCompound $aaContactDist $neigh | tee $llCompound
		}
	}

	distTh := "-30.0"	# AA not in contact (distance more than 30 Angstrom)
	for(int neigh=1; neigh < 5 ; neigh++ ) {
		llCompound := "$dir/likelihood.pdb_compound.neigh_$neigh.$distTh.txt"
		task( llCompound <- [pdbCompoundList, idMapPdbCompound, msasCompound], cpus := 1) {
			sys $cmdEpistasis likelihoodpdbinteract $snpeffConfig $genome $tree $msasCompound $pdbDir $idMapConfirmed $qhat $aaFreqs $qhat2 $aa2Freqs $pdbCompound $distTh $neigh | tee $llCompound
		}
	}
}

