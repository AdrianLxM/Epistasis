#!/usr/bin/env bds

#-------------------------------------------------------------------------------
# Epistasis project
#
#															Pablo Cingolani 2014
#-------------------------------------------------------------------------------

debug			:= false
dir				:= "$HOME/snpEff"

# Parameters
aaContactDist	:= 3.0	# Distance between AA to be considered 'in contact'
aaMinSepration	:= 25	# Number of AA separation to consider them for distance analysis
genome			:= "hg19"

# Data files
aaContact		:= "$dir/db/pdb/aa.contact.txt"
idMap			:= "$dir/db/multiz100way/idMap_ensemblId_refseq_pdbId.txt"
idMapConfirmed	:= "$dir/db/multiz100way/idMap_ensemblId_refseq_pdbId.confirmed.txt"
msa				:= "$dir/db/multiz100way/refGene.exonAA.fa"
pdbDir			:= "$dir/db/pdb/pdb_hires_human"
qhat			:= "$dir/db/multiz100way/Qhat.txt"
tree			:= "$dir/db/multiz100way/hg19.100way.nh"
snpeffConfig	:= "$dir/snpEff.config"

# Commands
cmdEpistasis	:= "java -Xmx4G -jar Epistasis.jar"

# Change parameters if we are debugging (smaller datasets)
if( debug ) {
	msa  = "$dir/db/multiz100way/head.fa"
	qhat = "$dir/db/multiz100way/Qhat_head.txt"
}

#-------------------------------------------------------------------------------
# Main
#-------------------------------------------------------------------------------

# Estimate matrix Q from sequence alignment and tree
task( qhat <- [tree, msa] )	sys $cmdEpistasis Qhat $tree $msa $qhat
wait

# Map Pdb <-> Genome
tmp := "$idMapConfirmed.tmp"
task( idMapConfirmed <- [pdbDir, tree, msa, idMap] ) {
	sys $cmdEpistasis mapPdbGenome $snpeffConfig $genome $pdbDir $idMap | tee $tmp 
	sys grep "^Confirmed IdMapping" $tmp | cut -f 2- | sort | uniq > $idMapConfirmed
}
wait

# Calculate amino acids "in contact" within the same protein
task( aaContact <- [pdbDir, idMapConfirmed] ) {
	sys $cmdEpistasis pdbdist $aaContactDist $aaMinSepration $pdbDir $idMapConfirmed \
		| tee \
		| grep "^Contact:" \
		| cut -f 2- \
		| sort \
		| uniq \
		> $aaContact
}

# TODO: Sample sequences of amino acids "in contact"

# TODO: Annotations of amino acids "in contact" (e.g. NextProt annotations)

# Done
wait
print("Done!\n")
