#!/usr/bin/env bds

#-------------------------------------------------------------------------------
# Epistasis project
#
#															Pablo Cingolani 2014
#-------------------------------------------------------------------------------

debug			:= false
dir				:= "$HOME/snpEff"

# Parameters
aaContactDist	:= 3.0	# Distance between AA to be considered 'in contact'
aaMinSepration	:= 25	# Number of AA separation to consider them for distance analysis
genome			:= "hg19"
genomeEnsembl	:= "GRCh37.75"

# Data files
aaContact		:= "$dir/db/pdb/aa.contact.txt"
aaContactSeq	:= "$dir/db/pdb/aa.contact.sequence.txt"
idMap			:= "$dir/db/multiz100way/idMap_ensemblId_refseq_pdbId.txt"
idMapConfirmed	:= "$dir/db/multiz100way/idMap_ensemblId_refseq_pdbId.confirmed.txt"
msa				:= "$dir/db/multiz100way/refGene.exonAA.fa"
pdbDir			:= "$dir/db/pdb/pdb_hires_human"
qhat			:= "$dir/db/multiz100way/Qhat.txt"
tree			:= "$dir/db/multiz100way/hg19.100way.nh"
snpeffConfig	:= "$dir/snpEff.config"

# Commands
#cmdEpistasis	:= "java -Xmx4G -jar Epistasis.jar"
cmdEpistasis	:= "java -Xmx20G -jar Epistasis.jar"

# Change parameters if we are debugging (smaller datasets)
if( debug ) {
	print("DEBUG MODE: Setting 'debug' paths\n")
	genome = "testHg19Chr1"
	msa  = "$dir/db/multiz100way/head.fa"
	qhat = "$dir/db/multiz100way/Qhat_head.txt"
}

#-------------------------------------------------------------------------------
# Main
#-------------------------------------------------------------------------------

# # Estimate matrix Q from sequence alignment and tree
print("\nEstimate Q: '$qhat'\n")
task( qhat <- [tree, msa] )	sys $cmdEpistasis Qhat $tree $msa $qhat
wait

# Map Pdb <-> Genome
print("\nMap Pdb entries to transcripts: '$idMapConfirmed'\n")
task( idMapConfirmed <- [pdbDir, tree, msa, idMap] )	sys $cmdEpistasis mapPdbGenome $snpeffConfig $genome $pdbDir $idMap | tee $idMapConfirmed
wait

# Calculate amino acids "in contact" within the same protein
print("\nCalculate AA in contact within proteins: $aaContact: '$aaContact'\n")
tmp := "$aaContact.tmp"
task( aaContact <- [pdbDir, idMapConfirmed] ) {
	sys $cmdEpistasis pdbdist $aaContactDist $aaMinSepration $pdbDir $idMapConfirmed > $tmp
	sys cat $tmp | grep "^Contact:" | cut -f 2- | sort | uniq > $aaContact
}
wait

# Add MSA sequences
print("\nAdd MSA information to AA in contact: '$aaContactSeq'\n")
task( aaContactSeq <- aaContact )	sys $cmdEpistasis addMsaSeqs $snpeffConfig $genome $tree $msa $idMapConfirmed $aaContact > $aaContactSeq
wait

# Claculate MI, conservation, etc.
aaMi := "$dir/db/pdb/aa.contact.sequence.mi.txt"
print("\nCalculating MI and conservation: '$aaMi'\n")
task( aaMi <- aaContactSeq )	sys $cmdEpistasis aaContactMi $aaContactSeq > $aaMi
wait

# Annotations of amino acids "in contact" (e.g. NextProt annotations)
aaNextProt := "$dir/db/pdb/aa.contact.nextProt.txt"
print("\nAnnotating with NextProt: '$aaNextProt'\n")
task( aaNextProt <- aaContactSeq )	sys $cmdEpistasis nextprot $snpeffConfig $genomeEnsembl $idMapConfirmed $aaContactSeq > $aaNextProt
wait

# Done
wait
print("\nDone!\n")
